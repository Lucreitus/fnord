/****************************************************************
 *
 * Copyright 1990, Brown University, Providence, RI
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
 *
 ****************************************************************/

/**********************************************************************
 *
 * RCH 9/19/90
 *
 * This is a pretty way of doing the visuals
 * for the twisted cubic
 **********************************************************************/

/*
 * make the cubic w/ a tube so we can see it 
 */
lenDom := Interval(-1, 1, 30);
circDom := Interval(0, 2pi, 6);

thick := 0.04;    /* radius of tube */

Tcubic
	 := R -> R^3 
	 : t -> [ t, t*t, t*t*t ]
	 ;

mixWin := widget type<-`view, set<-Axes;

widget type<-`vobject, 
   set<-(Tube(Tcubic, radius->thick)[lenDom><circDom]),
   color<-"red",
   parent<-`mixWin;

/*
 * now for the three sheets
 */

panel := widget type<-`window, units<-3;

a := widget Slider( 0, 1 ), init<-1, drag<-0,
  parent<-`panel, name<-"Parabaloid Sheet";
b := widget Slider( 0, 1 ), init<-0, drag<-0,
  parent<-`panel, name<-"Cubic Sheet";
c := widget Slider( 0, 1 ), init<-0, drag<-0,
  parent<-`panel, name<-"The t^3 vs t^3 sheet";

ShtDom := Grid( (-1,1,10), (-1,1,10) );

PSht 
   := R^2->R^3
   : [ u, v ] -> [ u, u**2, a*v ]
   ;
CSht 
   := R^2->R^3
   : [ u, v ] -> [ u,b*v, u**3 ]
   ;
OSht 
   := R^2->R^3
   : [ u, v ] -> [ c*v, u**2, u**3 ]
   ;


widget type<-`vobject, set<-( PSht ShtDom ), color<-"green",
   parent<-`mixWin;

widget type<-`vobject, set<-( CSht ShtDom ), color<-"blue",
   parent<-`mixWin;

widget type<-`vobject, set<-( OSht ShtDom ), color<-"bisque",
   parent<-`mixWin;






















